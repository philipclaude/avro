
#Find SuiteSparse libraries
FIND_LIBRARY( SUITESPARSE_UMFPACK umfpack HINTS $ENV{SUITESPARSE_DIR}/lib $ENV{SUITESPARSE_DIR}/UMFPACK/Lib $ENV{SUITESPARSE_LIB} REQUIRED )
FIND_LIBRARY( SUITESPARSE_AMD     amd     HINTS $ENV{SUITESPARSE_DIR}/lib $ENV{SUITESPARSE_DIR}/AMD/Lib     $ENV{SUITESPARSE_LIB} REQUIRED )
FIND_LIBRARY( SUITESPARSE_CAMD    camd    HINTS $ENV{SUITESPARSE_DIR}/lib $ENV{SUITESPARSE_DIR}/CAMD/Lib    $ENV{SUITESPARSE_LIB}          )
FIND_LIBRARY( SUITESPARSE_CHOLMOD cholmod HINTS $ENV{SUITESPARSE_DIR}/lib $ENV{SUITESPARSE_DIR}/CHOLMOD/Lib $ENV{SUITESPARSE_LIB}          )
FIND_LIBRARY( SUITESPARSE_COLAMD  colamd  HINTS $ENV{SUITESPARSE_DIR}/lib $ENV{SUITESPARSE_DIR}/COLAMD/Lib  $ENV{SUITESPARSE_LIB}          )
FIND_LIBRARY( SUITESPARSE_CCOLAMD ccolamd HINTS $ENV{SUITESPARSE_DIR}/lib $ENV{SUITESPARSE_DIR}/CCOLAMD/Lib $ENV{SUITESPARSE_LIB}          )
FIND_LIBRARY( SUITESPARSE_LIBRARY SuiteSparse  HINTS $ENV{SUITESPARSE_DIR}/lib $ENV{SUITESPARSE_DIR}/SUITESPARSE/Lib $ENV{SUITESPARSE_LIB} )
FIND_LIBRARY( SUITESPARSE_METIS   metis  HINTS $ENV{SUITESPARSE_DIR}/lib $ENV{SUITESPARSE_LIB} )

IF( NOT SUITESPARSE_LIBRARY )
  FIND_LIBRARY( SUITESPARSE_LIBRARY suitesparseconfig  HINTS $ENV{SUITESPARSE_DIR}/lib $ENV{SUITESPARSE_DIR}/SUITESPARSE/Lib $ENV{SUITESPARSE_DIR}/SuiteSparse_config $ENV{SUITESPARSE_LIB} REQUIRED )
ENDIF()

FIND_PATH( SUITESPARSE_AMD_INCLUDE     amd.h                HINTS $ENV{SUITESPARSE_DIR}/include $ENV{SUITESPARSE_DIR}/AMD/Include        $ENV{SUITESPARSE_INCLUDE} PATHS /usr/include/suitesparse REQUIRED )
FIND_PATH( SUITESPARSE_UMFPACK_INCLUDE umfpack.h            HINTS $ENV{SUITESPARSE_DIR}/include $ENV{SUITESPARSE_DIR}/UMFPACK/Include    $ENV{SUITESPARSE_INCLUDE} PATHS /usr/include/suitesparse REQUIRED )
FIND_PATH( SUITESPARSE_CONFIG_INCLUDE  SuiteSparse_config.h HINTS $ENV{SUITESPARSE_DIR}/include $ENV{SUITESPARSE_DIR}/SuiteSparse_config $ENV{SUITESPARSE_INCLUDE} PATHS /usr/include/suitesparse          )

SET( SUITESPARSE_INCLUDE_DIRS ${SUITESPARSE_AMD_INCLUDE} ${SUITESPARSE_UMFPACK_INCLUDE} )
IF( SUITESPARSE_CONFIG_INCLUDE )
  LIST( APPEND SUITESPARSE_INCLUDE_DIRS ${SUITESPARSE_CONFIG_INCLUDE} )
ENDIF()
LIST( REMOVE_DUPLICATES SUITESPARSE_INCLUDE_DIRS )

SET( SUITESPARSE_LIBRARIES ${SUITESPARSE_UMFPACK} ${SUITESPARSE_AMD} )
IF( SUITESPARSE_CHOLMOD )
  SET( SUITESPARSE_LIBRARIES ${SUITESPARSE_LIBRARIES} ${SUITESPARSE_CHOLMOD} )
ENDIF()
IF( SUITESPARSE_CAMD )
  SET( SUITESPARSE_LIBRARIES ${SUITESPARSE_LIBRARIES} ${SUITESPARSE_CAMD} )
ENDIF()
IF( SUITESPARSE_COLAMD )
  SET( SUITESPARSE_LIBRARIES ${SUITESPARSE_LIBRARIES} ${SUITESPARSE_COLAMD} )
ENDIF()
IF( SUITESPARSE_CCOLAMD )
  SET( SUITESPARSE_LIBRARIES ${SUITESPARSE_LIBRARIES} ${SUITESPARSE_CCOLAMD} )
ENDIF()
IF( SUITESPARSE_LIBRARY )
  SET( SUITESPARSE_LIBRARIES ${SUITESPARSE_LIBRARIES} ${SUITESPARSE_LIBRARY} )
ENDIF()
IF( SUITESPARSE_METIS )
  SET( SUITESPARSE_LIBRARIES ${SUITESPARSE_LIBRARIES} ${SUITESPARSE_METIS} )
ENDIF()

INCLUDE(FindPackageHandleStandardArgs)

FIND_PACKAGE_HANDLE_STANDARD_ARGS(SUITESPARSE  DEFAULT_MSG
                                  SUITESPARSE_LIBRARIES SUITESPARSE_INCLUDE_DIRS)

IF( NOT SUITESPARSE_FOUND )
  MESSAGE( "" )
  MESSAGE( "=================================" )
  MESSAGE( " avro will download and compile SuiteSparse for you. Compiling SuiteSparse is time consuming." )
  MESSAGE( " Alternatively, you may install the SuiteSparse with a package manager." )
  MESSAGE( " Or download and compile SuiteSparse and set \$SUITESPARSE_DIR to the root directory containing the MakeFile." )
  MESSAGE( " You can also set \$SUITESPARSE_INCLUDE and \$SUITESPARSE_LIB to separate suitesparse include and library directories." )
  MESSAGE( "=================================" )
  MESSAGE( "" )
ENDIF()

MARK_AS_ADVANCED(SUITESPARSE_INCLUDE_DIRS SUITESPARSE_LIBRARIES)
