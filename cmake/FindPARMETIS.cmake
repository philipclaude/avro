
 #
 # Find the ParMETIS includes and libraries
 #
 # ParMETIS Can be found at
 #     http://glaros.dtc.umn.edu/gkhome/metis/parmetis/overview
 #

FIND_PACKAGE(PkgConfig)

SET( PARMEITS_ARCH ${CMAKE_SYSTEM_NAME}-${CMAKE_SYSTEM_PROCESSOR} )

PKG_CHECK_MODULES(PARMETIS QUIET libparmetis)
PKG_CHECK_MODULES(METIS QUIET libmetis)

IF ( PARMETIS_FOUND )
    SET(PARMETIS_DEFINITIONS ${PARMETIS_CFLAGS_OTHER})
ELSE()
    FIND_PATH(PARMETIS_INCLUDE_DIRS parmetis.h
          HINTS $ENV{PARMETIS_DIR}/include
          PATH_SUFFIXES PARMETIS )

    FIND_LIBRARY(PARMETIS_LIBRARIES NAMES parmetis parmetis${PARMETIS_LIB_SUFFIX}
             HINTS $ENV{PARMETIS_DIR}/lib $ENV{PARMETIS_DIR}/build/${PARMEITS_ARCH}/libparmetis/ )

    FIND_LIBRARY(METIS_LIBRARIES NAMES metis metis${METIS_LIB_SUFFIX}
             HINTS $ENV{METIS_DIR}/lib $ENV{PARMETIS_DIR}/build/${PARMEITS_ARCH}/libmetis )
ENDIF()

IF( PARMETIS_INCLUDE_DIRS AND PARMETIS_LIBRARIES )
  # Get the parmetis major version number
  FILE(STRINGS "${PARMETIS_INCLUDE_DIRS}/parmetis.h" PARMETIS_MAJOR_VERSION REGEX "PARMETIS_MAJOR_VERSION" )
  STRING( REGEX MATCH "[0-9]+$" PARMETIS_MAJOR_VERSION ${PARMETIS_MAJOR_VERSION} )

  # parmetis 4 requires metis.h
  IF( ${PARMETIS_MAJOR_VERSION} GREATER 3 )

    IF ( METIS_FOUND )
        SET(METIS_DEFINITIONS ${METIS_CFLAGS_OTHER})
    ELSE()
        FIND_PATH(METIS_INCLUDE_DIRS metis.h
              HINTS $ENV{METIS_DIR}/include $ENV{PARMETIS_DIR}/metis/include $ENV{PARMETIS_DIR}/include
              PATH_SUFFIXES METIS )
    ENDIF()

    LIST( APPEND PARMETIS_INCLUDE_DIRS ${METIS_INCLUDE_DIRS} )
    LIST( REMOVE_DUPLICATES PARMETIS_INCLUDE_DIRS )

  ENDIF()
ENDIF( PARMETIS_INCLUDE_DIRS AND PARMETIS_LIBRARIES )

INCLUDE(FindPackageHandleStandardArgs)

FIND_PACKAGE_HANDLE_STANDARD_ARGS(PARMETIS  DEFAULT_MSG
                                  PARMETIS_LIBRARIES METIS_LIBRARIES PARMETIS_INCLUDE_DIRS)

LIST( APPEND PARMETIS_LIBRARIES ${METIS_LIBRARIES} )
LIST( REMOVE_DUPLICATES PARMETIS_LIBRARIES )
